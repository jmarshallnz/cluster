      SUBROUTINE PAM(NN,JPP,KK,X,DYS,JDYSS,VALMD,JTMD,NDYST,NSEND,NREPR,
     F NELEM,RADUS,DAMER,TTD,SEPAR,TTSYL,MED,OBJ,NCLUV,CLUSINF,SYLINF,
     F NISOL)
CC
CC    PARTITIONING AROUND MEDOIDS
CC
CC    CARRIES OUT A CLUSTERING USING THE K-MEDOID APPROACH.
CC
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION X(NN,JPP),VALMD(JPP),JTMD(JPP)
      DIMENSION NSEND(NN),NREPR(NN),NELEM(NN),NCLUV(NN)
      DIMENSION RADUS(NN),DAMER(NN),TTD(NN),SEPAR(NN),MED(KK)
      DIMENSION CLUSINF(KK,5),SYLINF(NN,4),OBJ(2)
      DIMENSION DYS(1+NN*(NN-1)/2),NISOL(KK)

      IF(JDYSS.EQ.1)GO TO 125
      JHALT=0
      CALL DYSTA(NN,JPP,X,DYS,NDYST,JTMD,VALMD,JHALT)
      IF (JHALT.EQ.0)GO TO 125
      JDYSS=-1
      RETURN
CC
  125 S=0.0
      NHALF=NN*(NN-1)/2+1
      L=1
  130 L=L+1
      IF(DYS(L).GT.S)S=DYS(L)
      IF(L.LT.NHALF)GO TO 130
      CALL BSWAP(KK,NN,NREPR,RADUS,DAMER,TTD,NHALF,DYS
     F,SKY,S,OBJ)
      CALL CSTAT(KK,NN,NSEND,NREPR,RADUS,DAMER,TTD,SEPAR,SKY,S,
     FNHALF,DYS,NCLUV,NELEM,MED,NISOL)
      DO 135 K=1,KK
      CLUSINF(K,1)=NREPR(K)
      CLUSINF(K,2)=RADUS(K)
      CLUSINF(K,3)=TTD(K)
      CLUSINF(K,4)=DAMER(K)
      CLUSINF(K,5)=SEPAR(K)
  135 CONTINUE
      IF(KK.LE.1)GO TO 140
      IF(KK.GE.NN)GO TO 140
      CALL DARK(KK,NN,NHALF,NCLUV,NSEND,NELEM,NREPR
     F,RADUS,DAMER,TTD,TTSYL,DYS,S,SYLINF)
  140 END
CC
CC
      SUBROUTINE DYSTA(NN,JPP,X,DYS,NDYST,JTMD,VALMD,JHALT)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION X(NN,JPP),DYS(1+NN*(NN-1)/2),JTMD(JPP),VALMD(JPP)
      PP=JPP
      NLK=1 
      DYS(1)=0.0
      DO 100 L=2,NN 
      LSUBT=L-1 
      DO 20 K=1,LSUBT 
      CLK=0.0 
      NLK=NLK+1 
      NPRES=0 
      DO 30 J=1,JPP 
      IF(JTMD(J).GE.0)GOTO 40 
      IF(X(L,J).EQ.VALMD(J))GOTO 30 
      IF(X(K,J).EQ.VALMD(J))GOTO 30 
   40 NPRES=NPRES+1 
      IF(NDYST.NE.1)GOTO 50 
      CLK=CLK+(X(L,J)-X(K,J))*(X(L,J)-X(K,J)) 
      GOTO 30 
   50 CLK=CLK+DABS(X(L,J)-X(K,J))
   30 CONTINUE
      RPRES=NPRES 
      IF(NPRES.NE.0)GOTO 60 
      JHALT=1 
      DYS(NLK)=-1.0
      GOTO 20 
   60 IF(NDYST.NE.1)GOTO 70 
      DYS(NLK)=DSQRT(CLK*(PP/RPRES)) 
      GOTO 20 
   70 DYS(NLK)=CLK*(PP/RPRES) 
   20 CONTINUE
  100 CONTINUE
      END 
CC
CC
      SUBROUTINE BSWAP(KK,NN,NREPR,DYSMA,DYSMB,BETER,HH,DYS
     F,SKY,S,OBJ)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER HH
      DIMENSION NREPR(NN),DYSMA(NN),DYSMB(NN),BETER(NN)
      DIMENSION DYS(HH),OBJ(2)
CC
CC    FIRST ALGORITHM: BUILD.
CC
      NNY=0
      DO 17 J=1,NN
      NREPR(J)=0
      DYSMA(J)=1.1*S+1.0
   17 CONTINUE
   20 DO 22 JA=1,NN
      IF(NREPR(JA).NE.0)GO TO 22
      BETER(JA)=0.
      DO 21 J=1,NN
      NJAJ=MEET(JA,J)
      CMD=DYSMA(J)-DYS(NJAJ)
      IF(CMD.GT.0.0)BETER(JA)=BETER(JA)+CMD
   21 CONTINUE
   22 CONTINUE
      AMMAX=0.
      DO 31 JA=1,NN
      IF(NREPR(JA).NE.0)GO TO 31
      IF(BETER(JA).LT.AMMAX)GO TO 31
      AMMAX=BETER(JA)
      NMAX=JA
   31 CONTINUE
      NREPR(NMAX)=1
      NNY=NNY+1
      DO 41 J=1,NN
      NJN=MEET(NMAX,J)
      IF(DYS(NJN).LT.DYSMA(J))DYSMA(J)=DYS(NJN)
   41 CONTINUE
      IF(NNY.NE.KK)GO TO 20
      SKY=0.
      DO 51 J=1,NN
      SKY=SKY+DYSMA(J)
   51 CONTINUE
      RNN=NN
      OBJ(1)=SKY/RNN
      IF(KK.EQ.1)GOTO 75
CC
CC    SECOND ALGORITHM: SWAP.
CC
   60 DO 63 J=1,NN
      DYSMA(J)=1.1*S+1.0
      DYSMB(J)=1.1*S+1.0
      DO 62 JA=1,NN
      IF(NREPR(JA).EQ.0)GO TO 62
      NJAJ=MEET(JA,J)
      IF(DYS(NJAJ).GE.DYSMA(J))GO TO 61
      DYSMB(J)=DYSMA(J)
      DYSMA(J)=DYS(NJAJ)
      GO TO 62
   61 IF(DYS(NJAJ).GE.DYSMB(J))GO TO 62
      DYSMB(J)=DYS(NJAJ)
   62 CONTINUE
   63 CONTINUE
      DZSKY=1.0
      DO 73 K=1,NN
      IF(NREPR(K).EQ.1)GO TO 73
      DO 72 JA=1,NN
      IF(NREPR(JA).EQ.0)GO TO 72
      DZ=0.
      DO 71 J=1,NN
      NJAJ=MEET(JA,J)
      NKJ=MEET(K,J)
      IF(DYS(NJAJ).NE.DYSMA(J))GO TO 70
      SMALL=DYSMB(J)
      IF(DYS(NKJ).LT.SMALL)SMALL=DYS(NKJ)
      DZ=DZ-DYSMA(J)+SMALL
      GO TO 71
   70 IF(DYS(NKJ).LT.DYSMA(J))DZ=DZ-DYSMA(J)+DYS(NKJ)
   71 CONTINUE
      IF(DZ.GE.DZSKY)GO TO 72
      DZSKY=DZ
      KBEST=K
      NBEST=JA
   72 CONTINUE
   73 CONTINUE
      IF(DZSKY.GE.0.)GO TO 75
      NREPR(KBEST)=1
      NREPR(NBEST)=0
      SKY=SKY+DZSKY
      GO TO 60
   75 RNN=NN
      OBJ(2)=SKY/RNN
      END
CC
CC
      SUBROUTINE CSTAT(KK,NN,NSEND,NREPR,RADUS,DAMER,TTD,SEPAR,Z,S,
     FHH,DYS,NCLUV,NELEM,MED,NISOL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER HH
      DIMENSION NCLUV(NN),NSEND(NN),NREPR(NN),NELEM(NN)
      DIMENSION SEPAR(NN),DAMER(NN),RADUS(NN),TTD(NN)
      DIMENSION DYS(HH),MED(KK),NISOL(KK)
      DO 130 J=1,NN
      IF(NREPR(J).EQ.1)GO TO 120
      DSMAL=1.1*S+1.0
      DO 110 K=1,NN
      IF(NREPR(K).EQ.0)GO TO 110
      NJAJ=MEET(K,J)
      IF(DYS(NJAJ).GE.DSMAL)GO TO 110
      DSMAL=DYS(NJAJ)
      KSMAL=K
  110 CONTINUE
      NSEND(J)=KSMAL
      GO TO 130
  120 NSEND(J)=J
  130 CONTINUE
      JK=1
      NPLAC=NSEND(1)
      DO 135 J=1,NN
      NCLUV(J)=0
      IF(NSEND(J).EQ.NPLAC)NCLUV(J)=1
  135 CONTINUE
      DO 145 JA=2,NN
      NPLAC=NSEND(JA)
      IF(NCLUV(NPLAC).NE.0)GO TO 145
      JK=JK+1
      DO 140 J=2,NN
      IF(NSEND(J).EQ.NPLAC)NCLUV(J)=JK
  140 CONTINUE
      IF(JK.EQ.KK)GO TO 148
  145 CONTINUE
CC
CC    ANALYSIS OF THE CLUSTERING.
CC
  148 DO 160 NUMCL=1,KK
      NTT=0
      RADUS(NUMCL)=-1.0
      TTT=0.0
      DO 150 J=1,NN
      IF(NCLUV(J).NE.NUMCL)GO TO 150
      NTT=NTT+1
      M=NSEND(J)
      NELEM(NTT)=J
      NJM=MEET(J,M)
      TTT=TTT+DYS(NJM)
      IF(DYS(NJM).GT.RADUS(NUMCL))RADUS(NUMCL)=DYS(NJM)
  150 CONTINUE
      RTT=NTT
      TTD(NUMCL)=TTT/RTT
      MED(NUMCL)=M
  160 CONTINUE
  230 RNN=NN
      IF(KK.NE.1)GO TO 240
      DAMER(1)=S
      NREPR(1)=NN
      GO TO 300
CC
CC    NUML = NUMBER OF L-CLUSTERS.
CC
  240 NUML=0
      DO 40 K=1,KK
CC
CC    IDENTIFICATION OF CLUSTER K:
CC       NEL=NUMBER OF OBJECTS
CC       NELEM=VECTOR OF OBJECTS
CC
      NEL=0
      DO 23 J=1,NN
      IF(NCLUV(J).NE.K)GO TO 23
      NEL=NEL+1
      NELEM(NEL)=J
   23 CONTINUE
      NREPR(K)=NEL
      IF(NEL.NE.1)GO TO 24
      NVN=NELEM(1)
      DAMER(K)=0.
      SEPAR(K)=1.1*S+1.0
      DO 250 J=1,NN
      IF(J.EQ.NVN)GO TO 250
      MEVJ=MEET(NVN,J)
      IF(SEPAR(K).GT.DYS(MEVJ))SEPAR(K)=DYS(MEVJ)
  250 CONTINUE
CC
CC    IS CLUSTER K     1) AN L-CLUSTER ?
CC                     2) AN L*-CLUSTER ?
CC
      IF(SEPAR(K).EQ.0.)GO TO 400
      NUML=NUML+1
  400 GO TO 35
   24 DAM=-1.
      SEP=1.1*S+1.0
      KAND=1
      DO 26 JA=1,NEL
      NVNA=NELEM(JA)
      AJA=-1.
      AJB=1.1*S+1.0
      DO 25 JB=1,NN
      JNDZ=MEET(NVNA,JB)
      IF(NCLUV(JB).EQ.K)GO TO 30
      IF(DYS(JNDZ).LT.AJB)AJB=DYS(JNDZ)
      GO TO 25
   30 IF(DYS(JNDZ).GT.AJA)AJA=DYS(JNDZ)
   25 CONTINUE
      IF(AJA.GE.AJB)KAND=0
      IF(DAM.LT.AJA)DAM=AJA
      IF(SEP.GT.AJB)SEP=AJB
   26 CONTINUE
      SEPAR(K)=SEP
      DAMER(K)=DAM
      IF(KAND.EQ.0)GO TO 35
      NUML=NUML+1
      IF(DAM.LT.SEP)GO TO 27
CC L-CLUSTER
      NISOL(K)=1
      GO TO 40
CC L*-CLUSTER
   27 NISOL(K)=2 
      GO TO 40
   35 NISOL(K)=0
   40 CONTINUE
  300 END

CC
CC
      SUBROUTINE DARK(KK,NN,HH,NCLUV,NSEND,NELEM,NEGBR
     F,SYL,SRANK,AVSYL,TTSYL,DYS,S,SYLINF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER HH
      DIMENSION NCLUV(NN),NSEND(NN),NELEM(NN),NEGBR(NN)
      DIMENSION SYL(NN),SRANK(NN),AVSYL(NN),DYS(HH),SYLINF(NN,4)
      NSYLR=0
      TTSYL=0.0 
      DO 100 NUMCL=1,KK 
      NTT=0 
      DO 30 J=1,NN
      IF(NCLUV(J).NE.NUMCL)GO TO 30
      NTT=NTT+1
      NELEM(NTT)=J
   30 CONTINUE
      DO 40 J=1,NTT 
      NJ=NELEM(J) 
      DYSB=1.1*S+1.0
      NEGBR(J)=-1
      DO 41 NCLU=1,KK
      IF(NCLU.EQ.NUMCL)GO TO 41
      NBB=0 
      DB=0.0
      DO 43 L=1,NN
      IF(NCLUV(L).NE.NCLU)GO TO 43
      NBB=NBB+1 
      MJL=MEET(NJ,L)
      DB=DB+DYS(MJL)
   43 CONTINUE
      BTT=NBB 
      DB=DB/BTT 
      IF(DB.GE.DYSB)GO TO 41
      DYSB=DB 
      NEGBR(J)=NCLU 
   41 CONTINUE
      IF(NTT.EQ.1)GO TO 50
      DYSA=0.0
      DO 45 L=1,NTT 
      NL=NELEM(L) 
      NJL=MEET(NJ,NL) 
      DYSA=DYSA+DYS(NJL)
   45 CONTINUE
      ATT=NTT-1
      DYSA=DYSA/ATT
      IF(DYSA.GT.0.0)GO TO 51 
      IF(DYSB.GT.0.0)GO TO 52
   50 SYL(J)=0.0
      GO TO 40
   52 SYL(J)=1.0
      GO TO 40
   51 IF(DYSB.LE.0.0)GO TO 53 
      IF(DYSB.GT.DYSA)SYL(J)=1.0-DYSA/DYSB
      IF(DYSB.LT.DYSA)SYL(J)=DYSB/DYSA-1.0
      IF(DYSB.EQ.DYSA)SYL(J)=0.0
      GO TO 54
   53 SYL(J)=-1.0 
   54 IF(SYL(J).LE.(-1.0))SYL(J)=-1.0 
      IF(SYL(J).GE.1.0)SYL(J)=1.0 
   40 CONTINUE
      AVSYL(NUMCL)=0.0
      DO 60 J=1,NTT 
      SYMAX=-2.0
      DO 70 L=1,NTT
      IF(SYL(L).LE.SYMAX)GO TO 70 
      SYMAX=SYL(L)
      LANG=L
   70 CONTINUE
      NSEND(J)=LANG 
      SRANK(J)=SYL(LANG)
      AVSYL(NUMCL)=AVSYL(NUMCL)+SRANK(J)
      SYL(LANG)=-3.0
   60 CONTINUE
      TTSYL=TTSYL+AVSYL(NUMCL)
      RTT=NTT 
      AVSYL(NUMCL)=AVSYL(NUMCL)/RTT 

      IF(NTT.GE.2)GOTO 75
      NSYLR=NSYLR+1
      SYLINF(NSYLR,1)=NUMCL
      SYLINF(NSYLR,2)=NEGBR(1)
      SYLINF(NSYLR,3)=0.0
      SYLINF(NSYLR,4)=NELEM(1)
      GOTO 100
   75 DO 80 L=1,NTT 
      NSYLR=NSYLR+1
      LPLAC=NSEND(L)
      SYLINF(NSYLR,1)=NUMCL
      SYLINF(NSYLR,2)=NEGBR(LPLAC)
      SYLINF(NSYLR,3)=SRANK(L)
      SYLINF(NSYLR,4)=NELEM(LPLAC)
   80 CONTINUE
  100 CONTINUE
      RNN=NN
      TTSYL=TTSYL/RNN 
   96 END


